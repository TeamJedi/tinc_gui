import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.poirsouille.tinc_gui"
        minSdkVersion 16
        targetSdkVersion 21
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_5
            targetCompatibility JavaVersion.VERSION_1_5
        }

        ndk {
            moduleName "tincd"
        }
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    def ndkDir = android.ndkDirectory
    def nkdBuildCmd = "$ndkDir/ndk-build" + (Os.isFamily(Os.FAMILY_WINDOWS) ? ".cmd" : "")
    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {

        commandLine "$nkdBuildCmd",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all'
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        commandLine "$nkdBuildCmd",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    /*
    // TODO: find a cleaner solution than un-commenting when needed....
    signingConfigs {
        release {
            storeFile file(System.console().readLine("\n\$ Enter keystore path: "))
            storePassword new String(System.console().readPassword("\n\$ Enter keystore password: "))
            keyAlias System.console().readLine("\n\$ Enter key alias: ")
            keyPassword new String(System.console().readPassword("\n\$ Enter key password: "))
        }
    }
    */
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            zipAlignEnabled true
            //signingConfig signingConfigs.release
        }
    }
}
